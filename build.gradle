plugins {
    id 'java'
    id "io.freefair.lombok" version "8.11"
    id "com.gradleup.shadow" version "8.3.2"
    id "io.papermc.paperweight.userdev" version "1.7.7"
    id "io.github.revxrsal.zapper" version "1.0.2"
}

group = 'net.solostudio'
version = '1.0.9'

repositories {
    mavenCentral()
    maven { url = 'https://oss.sonatype.org/content/groups/public/' }
    maven { url = "https://jitpack.io" }
    maven { url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
}

dependencies {
    implementation("io.github.revxrsal:lamp.bukkit:4.0.0-beta.23") {
        exclude(module: "lamp.common")
        exclude(module: "lamp.brigadier")
    }

    zap("mysql:mysql-connector-java:8.0.33")
    zap("io.github.revxrsal:lamp.common:4.0.0-beta.23")
    zap("io.github.revxrsal:lamp.brigadier:4.0.0-beta.23")
    zap("org.bstats:bstats-bukkit:3.0.2")
    zap("com.github.Anon8281:UniversalScheduler:0.1.6")
    zap("com.h2database:h2:2.3.232")
    zap("com.zaxxer:HikariCP:6.2.1")

    compileOnly("me.clip:placeholderapi:2.11.6")
    compileOnly("org.projectlombok:lombok:1.18.36")

    paperweight.paperDevBundle("1.21.3-R0.1-SNAPSHOT")
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

compileJava {
    options.compilerArgs += ["-parameters"]
}

zapper {
    libsFolder = "libs"
    relocationPrefix = "net.solostudio.vaultcher.libs"

    repositories { it.includeProjectRepositories() }

    relocate("org.bstats", "bstats")
    relocate("org.h2", "h2")
    relocate("com.github.Anon8281.universalScheduler", "universalScheduler")
}

shadowJar {
    exclude("lamp.lamp_pt.properties")
    exclude("lamp.lamp_en.properties")
    exclude("lamp.lamp_it.properties")
    exclude("lamp.lamp_fr.properties")
    exclude("lamp.lamp-bukkit_pt.properties")
    exclude("lamp.lamp-bukkit_it.properties")
    exclude("lamp.lamp-bukkit_fr.properties")
    exclude("lamp.lamp-bukkit_en.properties")
}